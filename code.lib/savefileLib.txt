# License
<<< EOF
#
# CFT - an interactive programmable shell for automation 
# Copyright (C) 2020-2023 Roar Foshaug
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, version 3 of the License.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>
#
>>> EOF
/License


# Readme for script
<<< EOF

--------------------------------------------------
OS independent editor functionality

To edit a file: Lib:e (File)
To page a file: Lib:m (File)

For windows runs notepad, for Linux there is
choice. 
 
--------------------------------------------------

>>> EOF
/Readme




# Edit file
# --
    P(1,Sys.savefile) => file
    P(2,true) => detach
    
    if (!detach) {
        # Need to run editor that we can wait for
        #
        if (Sys.isWindows) {
            Dir.run("cmd","/c","notepad",file.path)
        } else {
            Dir.run("nano",file.path)
        }
    } else {
        # regular processing
        
        if(Sys.isWindows) {
            npp = Dir("C:\Program Files (x86)\Notepad++")
            if (npp.exists) {
                Dir.runDetach(npp.path + "\notepad++.exe", file.path)
            } else {
                Dir.runDetach("cmd","/c","notepad",file.path)
            }
        } else {
            ShowLinuxEditor(file)
        }
    }
/e



# Page through file
# --
    P(1,Sys.savefile) =>file
    if(Sys.isWindows)
        Dir.run("cmd", "/c", "more", file.path)
    else
        Dir.run("more", file.path)
/m



# Page through list of lines
# --
	P(1) as List => lines
	count=0
	lines->line
		if (count >= Term.h-3) {
                        println
			readLine("Enter to continue, 'q' to abort")=>x
			error(x=="q","Aborting")
			count=0
		}
		println(line)
		count=count+1
	|
/mList
		

# Get file from lastResult if possible, otherwise error
# --
    x = Sys.lastResult
    if (getType(x)=="List") {
        files=Inner{x->e assert(getType(e)=="File") out(e)}
        error(files.empty,"No files in lastResult")
        Lib:MenuSelect(files,Lambda{P(1).name},null,true) => x
    }
    error(getType(x)!="File","Not a file")
    x
/GetLastResultFile



# Linux helper
# --
    P(1,"ls")=>program
    Dir.runCapture("which", program) => lines
    if (lines.length >= 1 && lines.first.contains("/"+program)) {
        true
    } else {
        false
    }
//LinuxHasProgram


# Show graphical file manager
# --
    P(1,Dir) => dir
    if (Sys.isWindows) {
        dir.run("explorer",dir.path)
    } else {
        if (LinuxHasProgram("nautilus")) {
            dir.runDetach("nautilus",dir.path)
        } else if (LinuxHasProgram("xfe")) {  
            dir.runDetach("xfe",dir.path)
        } else {
            error("Found no file manager")
        }
    }
/fileManager



# Update terminal size (if possible)
# --
    false =>updated

    if (Term.isTerminal) {
        if(!Sys.isWindows) {
            # Linux
            Dir.runCapture("which","stty").nth.contains("/stty") =>hasStty
            if(hasStty, Inner{
                Dir.runCapture("stty","size").nth.split =>list

                Term.h(list.nth(0).parseInt)
                Term.w(list.nth(1).parseInt)
                true =>updated
            })
        } else {
            # Windows (a bit slow)
            # 2021-08-18 RFO: some windows environments fail running powershell
            tryCatch ({
                Dir.runCapture("powershell","(Get-Host).UI.RawUI.WindowSize.Width").nth.parseInt =>w
                Dir.runCapture("powershell","(Get-Host).UI.RawUI.WindowSize.Height").nth.parseInt =>h
            }) => result
            if (!result.ok) {
                key="GetTerminalSize Windows Fails"
                if (Util:HasTimedOut(key,3600)) {
                    Lib:ShowWarn("GetTerminalSize() fails (windows) - using 80x24")
                    Util:SetTimeMark(key)
                }
                w=80
                h=24
            }
            Term.w(w)
            Term.h(h)
            true =>updated
        }
    }
    #if(updated, Term, null)
    Term
/GetTerminalSize
            


# Select Linux editor and remember selection for next time, open file in editor
# --
  P(1,Sys.savefile) =>file
    Dict
        .set("sublime",Lambda { P(1)=>file Dir.runDetach("subl",file.path) })
        .set("gedit", Lambda { P(1)=>file Dir.runDetach("gedit",file.path) })
        .set("leafpad",Lambda { P(1)=>file Dir.runDetach("leafpad",file.path) })
        .set("nano", Lambda { P(1)=>file Dir.run("nano", file.path) })
        .set("micro",Lambda { P(1)=>file Dir.run("micro", file.path) })
    => actions

   if(Db2:Get("Lib","linuxEditor")==null) {
        MenuSelect(actions.keys, Lambda { P(1) },"Select editor") =>ed
        Db2:Set("Lib","linuxEditor", ed)
    }


    ed = Db2:Get("Lib","linuxEditor")
    
    # invoke lambda (really a closure, since part of a Dict, but that doesn't matter here)
    actions.get(ed).call(file)
    
//ShowLinuxEditor



# Clear selected Linux editor
# --
    Db2:Set("Lib","linuxEditor",null)
    "Linux editor selection cleared"
/ClearEditor



# Open command shell in given directory
# --
    P(1) as Dir => dir
    if (Sys.isWindows) {
        dir.run("powershell -c start-process powershell".split)
    } else {
        dir.run("lxterminal") 
    }
/OpenCMD
        

# Get the private dir
# --
    Sys.homeDir.sub("private") =>d
    if(!d.exists) d.create
    d
/DirPrivate



# Create name of temp file under /tmp or under c:\temp if windows
# --
    P(1,"tmp") =>name 
    P(2,"txt") => type
    Util:Counter("Std.TmpFile.id") => x
    s=name+Std.Db.UUID+"-"+x+"."+type
    if (Sys.isWindows,
        Dir("c:\temp").file(s),
        Dir("/tmp").file(s) 
    )
/TmpFile


# Create temporary directory - remember to delete it when done
# --
    P(1,"tmp") => name
    if (Sys.isWindows,
        Dir("c:\temp\"+name+currentTimeMillis),
        Dir("/tmp/"+name+currentTimeMillis)
    ) =>d
    if (!d.exists) {
        d.create
    }
    d
/TmpDir


# Copy directory tree. Both fromDir and toDir must exist.
# --
    P(1) => fromDir
    P(2) => toDir
    P(3,false) => verbose
    P(4,true) => execute  # set false to do "dry run"
    P(5, Lambda{true}) => LInclude

    error(!fromDir.exists, "Invalid fromDir: " + fromDir.path)
    error(execute && !toDir.exists, "Invalid toDir: " + toDir.path)
    error(fromDir.path==toDir.path, "fromDir == toDir")

    fromDir.files->f 
        assert(LInclude.call(f))
        if(verbose) {
            println("FROM: " + f.path) 
            println("TO  : " + toDir.path)
        }
        if (execute) toDir.copy(f) 
    |
    fromDir.dirs->d 
        toDir.sub(d.name) => d2
        if(!d2.exists && execute) d2.create
        CopyDirTree(d,d2,verbose,execute,LInclude)
    |
/CopyDirTree



# Compare files from source tree to target tree, prints report to stdout
# --
    P(1) => fromDir
    P(2) => toDir
    P(3,true) => recurse

    error(!fromDir.exists, "Invalid fromDir: " + fromDir.path)
    error(!toDir.exists, "Invalid toDir: " + toDir.path)

    fromDir.files->f
        toDir.file(f.name) => f2
        if (f2.exists) {
            if (f2.length != f.length || f2.hash != f.hash) println("DIFFERS: " + f2.path)
        } else {
            println("NOT FOUND: " + f2.path)
        }
    |
    if (recurse) {
        fromDir.dirs->d 
            toDir.sub(d.name) => d2
            if(!d2.exists) {
                println("NO DIR: " + d2.path)
            } else {
                CompareTrees(d,d2,true)
            }
    }
/CompareTrees



# Print directory tree structure
# --
    P(1,Dir)=>dir

    # recursion control parameters
    P(2,0)=>nestCount
    P(3,List)=>output

    indent="|  ".times(nestCount)
    indent2="|  ".times(nestCount+1)
    indent2="|  ".times(nestCount+1)

    output.add(indent + "+ " + dir.name)

    error(output.length > 1000,"Over 1000 lines of text generated. Aborting.")

    Inner{
        files=dir.files
        line=indent2
        lineCount=0
        files->f 
            if (line.length+1+f.name.length >= Term.w) {
                output.add(line)
                lineCount=lineCount+1
                line=indent2
            }
            line=line + " " + f.name
            break(lineCount >= 5)
        |
        if (lineCount >= 5) {
            output.add(indent2 + " ...")
        } else {
            if (line.length > indent2.length) output.add(line)
        }
        output.add(indent2)
    }
    Inner{
        dir.dirs->d 
            reject(d.name.startsWith("."))  # ignore system directories
            ShowTree(d,nestCount+1,output)
    }

    output
/ShowTree
        


# Delete directory tree, asks for interactive confirm
# Leaves root directory
# --
    P(1)=>xdir
    Sequence(
        @
        @ ------------------------------------------
        @ ------------------------------------------
        @     Deleting directory tree confirm
        @ ------------------------------------------
        @
        xdir.path
        @
        @ ------------------------------------------
        @
    )->x println(x) |
    readLine("Are you really SURE? Type 'Yes'")=="Yes" =>ok
    error(!ok,"Aborting")
    
    xdir.allFiles->f f.delete |
    xdir.allDirs->d d.delete |
/DelDirTree



# Run external program and collect stdout, stderr and exitCode - returns intermediate Dict
# representing the running process, with a member closure called "wait", which in turn
# delivers a result Dict with the following fields:
#       cmd, stdin, stdout, stderr, exitCode
#
# Run external program in background.
#
# Example of use:
#     runProcess(...)  => a
#     runProcess(...)  => b
#     a.wait => aResult
#     b.wait => bResult
# --
    P(1,List("ls","-l")) => cmd
    P(2,List) => stdinLines
    P(3,false) => acceptErrors
    P(4,false) => showDebug

    if (getType(cmd) != "List") {
        cmd=List(cmd) # wrap as list
    }
    if (getType(stdinLines) != "List") {
        stdinLines=List(stdinLines) # wrap as List
    }
    Lib:TmpFile("in") =>inFile
    Lib:TmpFile("out") =>outFile
    Lib:TmpFile("err") =>errFile
    
    inFile.create(stdinLines)

    # The Dir.runProcess call
    process = Dir.runProcess(inFile,outFile,errFile,cmd)

    Dict
        .set("cmd", cmd)
        .set("acceptErrors", acceptErrors)
        .set("showDebug", showDebug)
        .set("stdin", inFile)
        .set("stdout", outFile)
        .set("stderr", errFile)
        .set("process", process)
        .set("isCompleted", Lambda {
            self.process==null || !self.process.isAlive
        })
        .set("wait", Lambda{
            # Delay until process terminated
            loop 
                break(self.process==null || !self.process.isAlive) 
                Sys.sleep(5) 
            |
            
            # Read temp files
            result = Dict
                .set("cmd", self.cmd)
                .set("stdin", self.stdin.read)
                .set("stdout", if(self.stdout.exists, self.stdout.read, List))
                .set("stderr", if(self.stderr.exists, self.stderr.read, List))
                .set("exitCode", if(self.process==null, -1, self.process.exitCode))

            # Delete temp-files
            # --
            # Note: Windows external processes do not allow files to be deleted
            # immediately even after returning isAlive() == false.
            
            Del = Lambda {
                P(1)=>f 
                count=0
                loop
                    if(f.exists && count <= 20) {
                        count=count+1
                        if (count>15) println("File delete failed, trying again: " + f.path)
                        Sys.sleep(5)
                        f.delete
                    } else {
                        break
                    }
            }
            Del.call(self.stdin)
            Del.call(self.stdout)
            Del.call(self.stderr)
            
            # Show debug
            
            FAIL = false
            if (result.exitCode != 0 && !self.acceptErrors) {
                FAIL = true
            }

            if(self.showDebug || FAIL) {
                Inner {  # "hide" loops inside inner block, so as not to affect return value of function
                    println("- cmd:")
                    result.get("cmd")->line println("    " + line) |

                    println("- exitCode = " + result.get("exitCode"))

                    println("- stdin:")
                    result.get("stdin")->line println("   # " + line) |

                    println("- stdout:")
                    result.get("stdout")->line println("   # " + line) |

                    println("- stderr:")
                    result.get("stderr")->line println("   # " + line) |
                    println
                }
            }
            
            error(FAIL,"run failed with error")
            
            # return value
            result

        })  # Lambda "wait"
    =>obj

    if (showDebug) Inner{
        println("--- runProcess result ---")
        Util:ShowDict(obj,true)
    }
    
    obj

/runProcess


# Run external program and collect stdout, stderr and exitCode - returns Dict
# with fields cmd, stdin, stdout, stderr, exitCode
# --
    P(1,List("ls","-l")) => cmd
    P(2,List) => stdinLines
    P(3,false) => acceptErrors
    P(4,false) => showDebug

    runProcess (cmd,stdinLines,acceptErrors,showDebug) => processDict
    
    # return value
    processDict.wait

/run





run(null,null,null,true)
/testRun



# Show a list of options
# --
    P(1,List) =>options
    P(2,Lambda { "name"}) =>LLabel
        options->x
            println("- " + LLabel.call(x))
/ShowOptions



# Select zero or more from list of options
# --
    P(1,List) =>options
    P(2,Lambda { "name"}) =>LLabel
    P(3,false) =>emptyMeansNone
    P(4,LLabel) => LValue
    
        sel=readLine("Enter exact or part of value?").trim
        if(emptyMeansNone && sel=="") {
            # empty list
            result=List
        } else Inner{
            # Look for exact match first, then if no hits, partial
            options->opt
                assert(LValue.call(opt)==sel)
                out(opt)
            | =>result
            if(result.empty) Inner{
                options->opt
                    assert(LValue.call(opt).contains(sel))
                    out(opt)
                | =>result
            }
        }
        result
/SelectOptions



# Display text with line under
# --
    P(1,"Title") =>title
    println
    println(title)
    println( "-".times(title.length) )
/Header



# Display info message
# --
    P(1,"Message") => msg
    
    println("* INFO: " + msg)
/ShowInfo


# Display warning message
# --
    P(1,"Message") => msg
    
    println
    println("### WARNING ### " + msg)
    println
/ShowWarn




# Select single value from list of options. Using lambda to extract name for each element.
# --
    P(1,List)=>options 
    P(2,Lambda {P(1)}) =>LLabel   ## display value
    P(3,"Select element") =>title 
    P(4,false) => emptyMeansNone
    P(5,LLabel) =>LValue          ## separate (narrower) value for matching input?

    error(getType(options) != "List", "Options must be a list (" + title + ")")
    error(options.length==0,"No elements in options list (" + title + ")")
    
    if(options.length==1) {
        options.first
    } else Inner {
        selectedElement=null
        loop
            println("")
            if(title != null) Header(title)
            if (emptyMeansNone) println("(Enter blank for none)")

            ShowOptions(options, LLabel)
            println("")
            SelectOptions(options, LLabel, emptyMeansNone, LValue) =>result
            
            if(result.length==1) {
                selectedElement=result.first
            } else {
                selectedElement=null
            }
            #println("SelectedElement = " + selectedElement)
            break(selectedElement != null || emptyMeansNone)
            println("** No match or not unique")
        | selectedElement
    }
/MenuSelect


# Ask user for input
# --
    P(1,"label") =>label 
    P(2) =>currVal
    println("")
    (label + if(currVal," ["+currVal+"]","") + " : ") =>prompt
    readLine(prompt) =>line
    if(line.trim=="", currVal, line)
/ReadValue




# Ask user for input
# --
    P(1,"label") =>label 
    P(2) =>currVal
    null =>gotResult
    loop
        ReadValue(label,""+currVal) =>val
        if(val.?parseInt) {val.parseInt =>gotResult}
        break(gotResult != null)
        println("** Integer required")
    |
    gotResult
/ReadValueInt



# Ask user for input
# --
    P(1,"label") =>label 
    P(2) =>currVal 
    P(3,"yes") =>trueVal 
    P(4,"no") =>falseVal

    null =>gotResult
    loop
        if(currVal != null, if(currVal, trueVal, falseVal), null) =>currStrVal
        
        ReadValue(label+" "+trueVal + "/" + falseVal,currStrVal) =>val
        if(val==trueVal) {true =>gotResult}
        if(val==falseVal) {false =>gotResult}
        break(gotResult != null)
        println("** Please enter '" + trueVal + "' or '"  + falseVal + "'")
    |
    gotResult
/ReadValueBoolean





# Get confirmation (true or false)
# --
    P(1,"Please confirm")=>label
    P(2,"yes")=>confirmString
    P(3,"no")=>defaultValue
    
    options=confirmString + "/" + defaultValue
    readLine(label + " " + options + " ("+defaultValue+")") => inp
    if (inp.trim=="") inp=defaultValue
    
    inp.toLower == confirmString.toLower
/Confirm



# Get confirmation or abort with error
# --
    P(1,"Continue")=>label
    P(2,"yes")=>confirmString
    P(3,"no")=>defaultValue
    
    label="!!! " + label
    println
    error(!Confirm(label,confirmString,defaultValue),"Aborting")
/HardConfirm



# If value is Dict, for each key found, return corresponding value (as list). Otherwise true if value found, false if not found
    P(1,List) =>lines
    P(2) =>value
    if(getType(value)=="Dict") Inner{
        # match keys and result corresponding values as list
        dict=value
        result=List
        lines->line
            dict.keys->key 
                if(line.contains(key)) {result.add(dict.get(key))}
        |
        result.unique
    } else Inner{   
        # check for single value
        lines->line assert(line.contains(value)) out(line) | _.length>0
    }
/LinesContain



# List available save files across all code directories
#
# --
    Sys.codeDirs.reverse->d
        Lib:Header(d.path)
        Inner {
            d.files(Glob("savefile*.txt"))->f 
                out(Str(f.name,f)) 
            | _.sort->x
                f=x.data
                println("   " + f.name.after("savefile").before(".txt"))
        }
/Scripts



# Current user (linux)
# --
    P(1,"") => pre
    P(2,"") => post
    if (!Sys.isWindows) {
        pre + Dir.runCapture("whoami").nth.trim + post
    } else {
        ""
    }
/LoggedInUser



# Search text files under current directory
# --
    P(1,Dir) => dir
    P(2,Input("Search term").get) => st
    P(3,Input("Enter partial file name, or glob pattern with '*'").get) => fname
    P(4,Input("Include sub-dirs y/n").setCurrCond("y")).get.toLower=="y" => subDirs
    
	if (subDirs) {
		Lib:Header("Note: Running as background job")
		proc=SpawnProcess(SymDict(dir,st,fname,subDirs), DoSearchTextFiles(dir,st,fname,subDirs))
		Sys.Jobs.add(proc,'Searching for "'+st+'" "'+fname+'" '+dir.path)
	} else {
		DoSearchTextFiles(dir,st,fname,subDirs)
	}
/SearchTextFiles

# DoSearchTextFiles
# --
	P(1) => dir
	P(2) => st
	P(3) => fname
	P(4) => subDirs

    glob=if(fname.contains("*"),Glob(fname),Glob("*"+fname+"*"))

    grep=Grep(st)

    totalCount=0
    if(subDirs,dir.allFiles(glob), dir.files(glob))->file 
        out(Int(file.lastModified,file)) 
    | _.sort-> x
        f=x.data
        match=grep.fileCount(f)
        totalCount=totalCount+match
        if (match>0) out(f)
    | => files
    
    dirs=Dict
    files->file
        dirs.set(file.dir.path,1)
    |
    dirCount=dirs.keys.length
    
    if (totalCount<2000 || dirCount==1) {
        # Show details
        Inner{
            files->f
                    grep.file(f)->line
                        println(Curses:TxtGreen + line.file.path + Curses:Reset)
						println("" + line.lineNumber + ": " + line)
						println
        }
    } else {
        # Show drilldown
        if (files.length > 0) Inner {
            Inner {
                pos=0
                files -> f
                    report(pos, f.path, Date.diff(Date(f.lastModified)).fmt)
                    pos=pos+1
            } -> line
                println(line)
            |
            println
            println("---> GOT " + totalCount + " HITS ...")
            println
            readLine("Drill down to directory of file (pos), ENTER to cancel").trim=>drillDown
            if (drillDown != "") {
                Sys.stdin("cd " + files.nth(drillDown.parseInt).dir.path)
            }
        }
        
    }

//DoSearchTextFiles

   
 

# Locate files under current directory
# --
    P(1,Dir) => dir
    P(2,Input("Enter partial file name, or glob pattern with '*'").get) => fname

    if (fname=="") fname="*"
    glob=if(fname.contains("*"),Glob(fname,true),Glob("*"+fname+"*",true))
    
    dir.allFiles(glob)->f
        out(Int(f.lastModified, f))
    | _.sort->x
        out(x.data)
    | => files

    if (files.length > 0) Inner {
        Inner {
            pos=0
            files -> f
                report(pos, f.path, Date.diff(Date(f.lastModified)).fmt)
                pos=pos+1
        } -> line
            println(line)
        |
        println
        readLine("Edit file (pos), ENTER to cancel").trim=>pos
        if (pos != "") {
            Lib:e(files.nth(pos.parseInt))
        }
    }
/LocateFiles



# Identify all file types under current dir
# --
	P(1,Dir) => dir
	Dir.allFiles->f out(f.name.afterLast("."))
	| _.unique.sort
/GetFileTypes
    
    

# Copy file into "clipboard"
# --
    P(1,Dir)=>currentDir
    P(2,false)=>deleteOnPaste

    data=Sys.lastResult
    files=List
    
    if (getType(data)=="File") {
         files.add(data)
    } else if (getType(data)=="List") Inner {
        data->x
            if (getType(x)=="File") {
                files.add(x)
            }
    }
    println("XXX=" + files.length)
    if (files.empty) {
        Db2:Set("Lib","ClipboardFiles",null)
        error("Can only copy single File or list of File to clipboard")
    }

    Db2:Set("Lib","ClipboardFiles",files)
    Db2:Set("Lib","ClipboardFilesDeleteOnPaste", deleteOnPaste)
    Inner {
        Lib:Header("Files stored to clipboard")
        files->f println(f.path)
        if (deleteOnPaste) {
            println println("** NOTE: originals are deleted on paste")
        }
    }
    "Copied " + files.length + " " + if(files.length==1,"file","files")
/ClipboardCopy



# Paste file from "clipboard"
# --
    P(1,Dir) => currentDir
    
    Db2:Get("Lib","ClipboardFiles", null) => files
    error(files==null, "No files found")

    Lib:Header("Files in clipboard")
    Inner{
        files->file println(file.path)
    }

    deleteOnPaste=Db2:Get("Lib","ClipboardFilesDeleteOnPaste",false)

    yesToAll=false
    files->file
        if (!yesToAll) {
            readLine(if(deleteOnPaste,"Move ","Copy ") + file.path + " to " + currentDir.path + "? (yes/no/y/n/all/none)").toLower => confirm

            error(confirm=="none", "Aborting paste!")
            assert(confirm=="yes" || confirm=="y" || confirm=="all")
            if (confirm=="all") yesToAll=true
        }
        println(if(deleteOnPaste,"Moving ","Copying ") + file.path)
        currentDir.copy(file)
        if (deleteOnPaste) file.delete
    |
    
/ClipboardPaste


# Diff files
# --
    P(1) as File => a
    P(2) as File => b
    if (Sys.isWindows) {
		Lib:TmpFile("tmp","ps1") => script
		script.create(Sequence(
			@ $file1 = '<<a.path>>'
			@ $file2 = '<<b.path>>'
			@ Compare-Object -ReferenceObject $(Get-Content $file1) -DifferenceObject $(Get-Content $file2) -CaseSensitive
		).mergeExpr)
        Dir.runCapture("powershell",script.path) => result
		script.delete
		result
    } else {
        Dir.run("diff",a.path,b.path)
    }
/DiffFiles



# Remove files and dirs. 
# Service function for "rm" shell command, called via mRm lambda in CFT.props
# --
    P(1) as List => filesAndDirs

    filesAndDirs->x
        if (getType(x)=="File") {
            ok = x.delete
            report(ok,"File",x.path)
        } else if (getType(x)=="Dir") {
            ok = x.delete
            if (ok) {
                report(ok,"Dir",x.path)
            } else {
                if (Lib:Confirm("Confirm deleting " + x.path + " with content") ) {
                    {x.allFiles->f ok=f.delete report(ok,"File",f.path)}
                    {x.allDirs->d ok=d.delete report(ok,"Dir",d.path)}
                    {x->d ok=d.delete report(ok,"Dir",d.path)}
                }
            }
        }
//RmFilesAndDirs


# Symbol set
# --
    P(1) as String => symbol

    Sys.lastResult => value

    if (getType(value) != "Dir" && getType(value) != "File") {
        Lib:ShowWarn("Sys.lastResult not Dir or File, type=" + getType(value))
        error(readLine("Press y to continue, otherwise abort").toLower != "y","Aborting")
    }

#    if (getType(value) == "Dir" || getType(value) == "File") {
#        Lib:Header("Protection?")
#        println("Should the " + getType(value) + " reference be protected against destructive operations?")
#        readLine("Enter non-blank string to protect it").trim => reason
#        if (reason != "") {
#            value.protect(reason)
#        }
#    }

    Db2:Get(Sys.scriptId,"symbolDict",Dict) => data
    time=currentTimeMillis
    data.set(symbol,SymDict(value,time))
    Db2:Set(Sys.scriptId,"symbolDict",data)
    
    # Show all symbols
    println
    Inner { Lib:Symbols(false)->line println(line) }
    println
    value
//SymbolSet



# Symbol get
# --
    P(1) as String => symbol
    Db2:Get(Sys.scriptId,"symbolDict",Dict) => data
    if (data.has(symbol)) {
        data.get(symbol) => entryDict
        entryDict.time=currentTimeMillis
        Db2:Set(Sys.scriptId,"symbols",data)
        entryDict.value
    } else {
        null
    }
//SymbolGet

# Show symbols (helper)
# --
    Db2:Get(Sys.scriptId,"symbolDict",Dict)=>data
    data.keys.sort->key
        data.get(key)=>entryDict
        value=entryDict.value
        s=""+value
        if (getType(value)=="File" || getType(value)=="Dir") s=value.path
        report(key,getType(value),s)
//SymbolsReport   


# Show and optionally delete symbols
# --
    P(1,true)=>edit
    loop
        Lib:Header("Defined symbols")
        Inner {
            SymbolsReport->line println(line)
        }
        break(!edit)
        println
        readLine("Enter symbol to delete, Enter for none") => line
        break(line=="")
        Db2:Get(Sys.scriptId,"symbolDict",Dict) => data
        data.remove(line.trim)
        Db2:Set(Sys.scriptId,"symbolDict",data)
        println
//Symbols


# Hash a number of files
# Called by mHash in CFT.props
# --
    space=false
    P(1) as List -> f
        if (space) out("")
        out(f.path)
        out(f.hash)
        space=true
//HashFiles


# Search through a list of files
# Called by mGrep in CFT.props
# --
    P(1) => str
    P(2) => fileList
    
    grep=Grep(str)
    fileList->file
        grep.file(file)->line
            report(line.file.name, line.lineNumber, line)
//GrepFiles



# Append to history - defined in CFT.props 'historyCommand' called from Java 
# --
	P(1) as Dir => dir
	P(2) as String => command
	time=Date
	
	if (command.trim!="" && !command.startsWith("?") && !command.startsWith("cd") && !command.startsWith("ls")) {
		data=SymDict(dir,command,time)
		list=Db2:Get(Sys.scriptId,"History",List)
		list.add(data)
		list=list.last(100)
		Db2:Set(Sys.scriptId,"History",list)
	}
//HistoryAppend



# Helper func for HistoryShow
# --
	P(1) as List => lines
	Sys.stdin(lines)
	lines
//HistoryExecuteLines


# Format history data as lines
# --
	P(1) as List => list
	
	count=0
	list->dict
		out(("   "+count).last(3) + " " + Curses:TxtGreen+dict.dir.path+Curses:Reset)
		out("       " + dict.command)
		count=count+1
//ShowHistoryData

# Show history
# --
	list=Db2:Get(Sys.scriptId,"History",List).last(100)

	filterStr=null
	loop
		if (filterStr != null) {
			list=Inner{
				list->dict
					include=dict.dir.path.contains(filterStr) || dict.command.contains(filterStr)
					assert(include)
					out(dict)
			}
		}

		lines=ShowHistoryData(list)
		Lib:Header("History")
		Inner {
			lines->line println(line)
		}
		println
		readLine("!N to repeat or N to go to dir, Enter to abort, any to narrow") => cmd
		if (cmd.trim=="") {
			break
		} else if (cmd.?parseInt) {
			n=cmd.parseInt
			dict=list.nth(n)
			HistoryExecuteLines(List("cd " + dict.dir.path))
			break
		} else if (cmd.startsWith("!") && cmd.sub(1).?parseInt) {
			n=cmd.sub(1).parseInt
			dict=list.nth(n)
			HistoryExecuteLines(List("cd " + dict.dir.path, dict.command))
			break
		} else {
			filterStr=cmd
		}
	|
	true
	
/HistoryShow


# Platform independent "which" implementation
# --
	P(1) => str
	if (Sys.isWindows) {
		Dir.run("powershell","-c","Get-Command " + str)
	} else {
		Dir.run("which", str)
	}
/Which
