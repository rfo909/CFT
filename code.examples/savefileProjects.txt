# License
<<< EOF
#
# CFT - an interactive programmable shell for automation 
# Copyright (C) 2020-2022 Roar Foshaug
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, version 3 of the License.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>
#
>>> EOF
/License




# Readme with example projects.txt file
# --
<<< EOF

Projects script
---------------
To use this script, we define projects in a text file, projects.txt under "private"
dir, as defined by Lib:DirPrivate.

The ### marker is followed by a project name, and then follows settings.

Single "#" at start of lines are comments.

Example:
---

### CFTJava

Dir: /home/xyz/CFT
Type: java
IgnoreDir: .git


### OtherProject

Dir: /home/xyz/OtherProject
Type: html
Type: css
Type: js




Configuration
-------------
The following settings are allowed, each can be repeated:

Dir: /home/xyz/CFT      ### string
Type: txt               ### converted to glob-expression *.(type)
Files: *.java           ### glob-expression
Ignore: *test*          ### glob-expression to ignore certain files
IgnoreDir: /.git        ### substring matching against path


There can be multiples of every line, so if we want to scan different directories in one go,
just add a second "Dir:" line.

>>> EOF
//ReadmeLines


# Readme
# --
    ReadmeLines->line println("| " + line)
/Readme

# The projects.txt file 
# --
    (Lib:DirPrivate).file("projects.txt") => f
    if (!f.exists || DataFile(f,"###").keys.length==0) {
        readme=Lambda{ReadmeLines->line out("# "+line)}
        Inner{
            Sequence(
                @
                @ << readme.call >>
                @
                @ # Example project (auto-generated << Date.fmt >>)
                @
                @ ### CFTJava
                @ 
                @ Dir: << Sys.homeDir.path >>
                @ Type: java
            )->line
                line.mergeExpr->s out(s)
        } => lines
        f.append(lines)
    }
    f
/ConfigFile   




# Projects config as DataFile
# --
    ConfigFile =>f
    DataFile(f,"###").comment("#")
//ProjectsFile


# Edit config file (see Readme)
# --
    Lib:e(ConfigFile)
/EditConfig



# List projects and select 
# --
    Db2:Get("Projects","Project") => result
    
    ProjectsFile.keys => names

    Lambda { P(1) } =>showName
    loop
        println
        Lib:Header ("Select project (" + result + ")")
        Lib:ShowOptions (names, showName)
        Lib:SelectOptions (names, showName, true) =>resultList
        if(resultList.length>1, Inner{println("** not unique **") null =>result} )
        if(resultList.length==1, Inner{resultList.nth =>result} )
        break(result != null)
    |
    Db2:Set("Projects","Project", result)
    result
//SelectProjectName


# Current project name
# --
    pName=Db2:Get("Projects","Project")
    if (pName==null) pName=SelectProjectName
    pName
//CurrProjectName


# Get current project as Dict
# --        
    pName=CurrProjectName
    
    ProjectsFile.get(pName) =>dataLines
    if(dataLines.length==0) {
        SelectProjectName
        pName=CurrProjectName
        ProjectsFile.get(pName) =>dataLines
    }

    dict=Dict
    dataLines->line
        line.before(":").trim =>field
        line.after(":").trim =>val
        dict.get(field,List).add(val)
    |
    dict
//CurrProject





# Produce list of files to search
# --
    CurrProject =>pr
    
    # Directories
    # --
    dirs = pr.get("Dir",List)
        
    
    # File matching by type
    # --
    pr.get("Type",List)->t 
        out(Glob("*."+t) )
    | => fileGlobs
    
    pr.get("Files",List)->f  ## glob strings
        fileGlobs.add(Glob(f))
    |
    
    # File ignores
    # --
    pr.get("Ignore",List)->i  ## Glob
        out(Glob(i))
    | => fileIgnores
    
    # Dir ignores
    # --
    dirIgnores = pr.get("IgnoreDir",List) ## substrings


    fileOk = Lambda {
        P(1)=>file
        P(2)=>ignoreGlobList
        
        ok=true
        ignoreGlobList->glob
            if (glob.match(file)) ok=false
        |
        ok
    }

    pathOk = Lambda{
        P(1)=>s 
        P(2)=>ignoreDirList
        
        ok=true
        ignoreDirList->pattern
            if (s.contains(pattern)) ok=false
        |
        ok
    }
    
    # 
    dirs->dirName
        fileGlobs->glob
            assert(Dir(dirName).exists)
            Dir(dirName).allFiles(glob)->f 
                assert(pathOk.call(f.path, dirIgnores))
                assert(fileOk.call(f,fileIgnores))
                out(f)
    | _.unique 
//TextFiles





# Search through TextFiles for pattern - delivers lines
# --
    P(1,Grep) =>grep
    
    filter=Db2:Get("Projects","FileFilter",null)

    TextFiles => textFiles
    
    # Count matches first
    textFiles->tf 
        assert(filter==null || file.name.contains(filter))
        out(grep.fileCount(tf)) 
    | _.sum =>count
    
    # If too many hits, just cancel
    error(count>10000, "Search matches " + count + " lines - not shown")

    textFiles->tf
        assert(filter==null || file.name.contains(filter))
        grep.file(tf)->line 
            out(line)
//DoSearch



# Search through TextFiles for pattern
# Returns formatted report
# -- 
    P(1,Grep) =>grep
    DoSearch(grep)->line
            report(line.file.name, line.lineNumber, line)
//Search





# Limit search to file names containing a certain string
# --
    readLine("Enter file filter string").trim =>str
    if(str=="",null,str) =>val
    Db2:Set("Projects","FileFilter",val)
/SetFileFilter


# Show info on current FileFilter (if defined)
# --
    Db2:Get("Projects","FileFilter") =>filter
    if(filter!=null, Inner{
        println
        println("** Filtering file names on: " + '"' + filter + '"')
        println
    })

/ShowFileFilter


# Clear file filter
# --
    Db2:Set("Projects","FileFilter",null)
/ClearFileFilter







# Search with one parameter
# --
    ShowFileFilter
    Input("Search1").get =>a
    Search(Grep(a))
/S


# Search with two parameters
# --
    ShowFileFilter
    Input("Search2a ... ").get =>a
    Input("... AND Search2b").get =>b
    Search(Grep.match(a).match(b))
/S2


# Search with three parameters
# --
    ShowFileFilter
    Input("Search3a ... ").get =>a
    Input("... AND Search3b").get =>b
    Input("... AND Search3c").get =>c
    Search(Grep.match(a).match(b).match(c))
/S3

# Search with one positive and one negative parameter
# --
    ShowFileFilter
    Input("Search1").get =>a
    Input("Search1n (reject)").get =>b
    Search(Grep(a).reject(b))
/SN

# Locate file by partial name
# --
    P(1,"java") =>fn

    TextFiles->tf
        assert(tf.name.contains(fn))
        out(tf)
//FileLocator


# Get file content around a given line number as List of lines
# --
    P(1,Input("File name or glob pattern").get) => globPattern
    P(2,Input("File line number").get.parseInt) => lineNo
    P(3,Input("Number of lines").get.parseInt) => numLines

    glob=Glob(globPattern)

    TextFiles->tf
        assert(glob.match(tf))
        out(tf)
    | => files

    error(files.length==0, "No matching files found")
    if (files.length == 1) {
        file=files.first
    } else {
        Lib:MenuSelect(files,Lambda{P(1).path},"Select file", true) => file
        error(file==null,"Aborting: no file selected")
    }
    file.read => lines

    delta=(numLines/2).i  # division always returns float!!
    lines.keep(lineNo-delta, lineNo+delta)
    
/gfc


# Show file content around a given line number, report style
# --
    Inner {
        gfc->line
            report(line.lineNumber, line)
    }->line 
        println(line)
    |
    true
/sfc

# FileLocator, interactive, sorted presentation, newest first
# --
    P(1,Input("Enter part of file name").get) => fn

    FileLocator(fn)->file
        out(Int(file.lastModified, file))
    | _.sort->x 
        out(x.data)
    | => fileList
    
    fileList->file 
        report(file.path, file.length, Date(file.lastModified).fmt)
    | =>lines  # the report(...) outputs
    

    # Display report lines, and create line numbers
    0 =>lineNo
    lines->line
        # Pad lineNo to 3 characters
        Inner{s="   "+lineNo loop break(s.length>=3) s=" "+s | s} =>lineNoStr
        
        println(lineNoStr + ": " + line)
        lineNo+1 =>lineNo
    |
    println
    readLine("Enter number to edit, or blank to skip") =>inp
    if(inp != "", Inner{
        num=inp.parseInt
        println("Editing file " + fileList.nth(num).path)
        Lib:e (fileList.nth(num))
    })

    fileList->f out(f.path)
/FL


# Show text files (that are being searched)
# --
    TextFiles
/TF


# Display current project
# --
    CurrProject =>pr
    println
    Lib:Header("Current project")
    println(CurrProjectName)
    println
    pr.keys->key
        str=Inner{pr.get(key,List).concat(" ")}
        println(key+": " + str)
    |
    println
    true
/curr


# Change project
# --
    SelectProjectName
    curr
/ch
